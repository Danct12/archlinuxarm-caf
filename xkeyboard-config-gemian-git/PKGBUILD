# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Evan Anderson <evananderson@thelinuxman.us>

pkgname=xkeyboard-config-geminipda-git
_pkgname=xkeyboard-config
pkgver=r1932.c258c2d
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="https://www.freedesktop.org/wiki/Software/XKeyboardConfig"
makedepends=('git' 'intltool' 'xorg-xkbcomp' 'libxslt')
provides=('xkbdata' 'xkeyboard-config')
replaces=('xkbdata')
conflicts=('xkbdata' 'xkeyboard-config')
source=('git+https://github.com/gemian/xkeyboard-config#branch=buster')
sha256sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${_pkgname}

    ./configure --prefix=/usr \
        --with-xkb-base=/usr/share/X11/xkb \
        --with-xkb-rules-symlink=xorg \
        --enable-compat-rules=yes
    make
}

package() { 
    cd ${_pkgname}

    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/X11/xkb/compiled"

    install -m755 -d "${pkgdir}/var/lib/xkb"
    install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
