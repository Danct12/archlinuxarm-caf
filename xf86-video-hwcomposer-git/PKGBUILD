_pkgname=xf86-video-hwcomposer
pkgname=$_pkgname-git
pkgver=r137.6083d8b
pkgrel=1
pkgdesc="X.Org driver for Android devices rendering through HWComposer API"
url="https://github.com/NotKit/$_pkgname"
arch=('x86_64' 'aarch64')
license=('MIT')
depends=('libepoxy' 'drihybris' 'libhybris' 'glamor-hybris')
makedepends=('git' 'pixman' 'xorg-server-devel' 'drihybrisproto')
source=("git+https://github.com/NotKit/$_pkgname#branch=renderthread")
md5sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$_pkgname"

	# Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
	# With them, module fail to load with undefined symbol.
	# See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
	export CFLAGS=${CFLAGS/-fno-plt}
	export CXXFLAGS=${CXXFLAGS/-fno-plt}
	export LDFLAGS=${LDFLAGS/,-z,now}
	export CPPFLAGS="$CPPFLAGS -I/usr/include/android -DMESA_EGL_NO_X11_HEADERS"

	NOCONFIGURE=1 ./autogen.sh

	./configure \
		--build=$CBUILD \
		--host=$CHOST \
		--prefix=/usr \
		--enable-drihybris \
		--enable-glamor-hybris
    make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
